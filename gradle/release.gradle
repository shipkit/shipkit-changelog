// docs: https://plugins.gradle.org/docs/publish-plugin
gradlePlugin {
    plugins {
        changelog {
            id = 'org.shipkit.shipkit-changelog'
            implementationClass = 'org.shipkit.changelog.ChangelogPlugin'
        }
        gitHubRelease {
            id = 'org.shipkit.shipkit-gh-release'
            implementationClass = 'org.shipkit.gh.release.GitHubReleasePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/shipkit/shipkit-changelog'
    vcsUrl = 'https://github.com/shipkit/shipkit-changelog.git'
    plugins {
        changelog {
            displayName = 'Shipkit changelog plugin'
            description = 'Generates changelog based on ticked IDs found in commit messages and GitHub pull request information'
            tags = ['ci', 'shipkit', 'changelog']
        }
        gitHubRelease {
            displayName = 'Shipkit GitHub release plugin'
            description = 'Posts a release to GitHub using the REST API'
            tags = ['ci', 'shipkit', 'github', 'release']
        }
    }
}

ext.'gradle.publish.key' = System.getenv('GRADLE_PUBLISH_KEY')
ext.'gradle.publish.secret' = System.getenv('GRADLE_PUBLISH_SECRET')

if (ext.'gradle.publish.key' && ext.'gradle.publish.secret') {
    println "Gradle Plugin Portal environment variables: " +
            "key=${ext.'gradle.publish.key'.substring(0, 3)}, secret=${ext.'gradle.publish.secret'.substring(0, 3)}"
}

tasks.named("generateChangelog") {
    previousRevision = project.ext.'shipkit-auto-version.previous-tag'
    readOnlyToken = System.getenv("GITHUB_TOKEN")
    repository = "shipkit/shipkit-changelog"
}

tasks.named("githubRelease") {
    dependsOn tasks.named("generateChangelog")
    repository = "shipkit/shipkit-changelog"
    changelog = tasks.named("generateChangelog").get().outputFile
//    After publishing new version of the plugin remove comment on 'newTagRevision' below:
//    newTagRevision = System.getenv("GITHUB_SHA")
    writeToken = System.getenv("GITHUB_TOKEN")
}
