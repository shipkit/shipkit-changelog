plugins {
    id 'org.shipkit.shipkit-auto-version' version "0.0.32"
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'groovy'
    id 'idea'
    id 'maven-publish'
}

group = "org.shipkit"

sourceCompatibility = 1.8

repositories {
    jcenter()
}

apply from: "$rootDir/gradle/integ-test.gradle"
apply from: "$rootDir/gradle/release.gradle"

dependencies {
    //small (30kb), stable (no changes since 2017), zero-dependencies library for JSON:
    implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"

    testImplementation 'junit:junit:4.12'

    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
    testImplementation "cglib:cglib-nodep:3.3.0" //mocking concrete classes with spock
    testImplementation "org.objenesis:objenesis:3.1" //as above

    testImplementation gradleTestKit()
}

publishing { // docs: https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        // Run this for local testing: "./gradlew publishMavenPublicationToMavenLocal"
        maven(MavenPublication) {
            from components.java
        }
    }
}
