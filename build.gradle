buildscript {
    repositories {
        mavenLocal()        // for local testing
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.shipkit:shipkit-auto-version:1.+"
        classpath "org.shipkit:shipkit-changelog:1.+"
        classpath "com.gradle.publish:plugin-publish-plugin:0.14.0"
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'

apply plugin: 'com.gradle.plugin-publish'

apply plugin: 'org.shipkit.shipkit-auto-version'
apply plugin: 'org.shipkit.shipkit-changelog'
apply plugin: 'org.shipkit.shipkit-github-release'

group = "org.shipkit"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

apply from: "$rootDir/gradle/integ-test.gradle"
apply from: "$rootDir/gradle/release.gradle"

dependencies {
    //small (30kb), stable (no changes since 2017), zero-dependencies library for JSON:
    implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"

    testImplementation 'junit:junit:4.13.2'

    testImplementation "org.spockframework:spock-core:2.0-groovy-3.0"
    testImplementation "cglib:cglib-nodep:3.3.0" //mocking concrete classes with spock
    testImplementation "org.objenesis:objenesis:3.2" //as above

    testImplementation gradleTestKit()
}

publishing { // docs: https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        // Run this for local testing: "./gradlew publishMavenPublicationToMavenLocal"
        maven(MavenPublication) {
            from components.java
        }
    }
}

idea {
    module {
        excludeDirs += file(".shelf")
    }
}